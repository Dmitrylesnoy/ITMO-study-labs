plugins {
    id 'application'
    id 'java'                          // Явно включаем Java-плагин
    id 'io.freefair.lombok' version '8.12'
    id 'org.openjfx.javafxplugin' version '0.0.14'

}

group = 'lab8.client'
// version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

javafx {
    version = '17'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation libs.guava
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.0'
    implementation 'org.glassfish.jaxb:jaxb-xjc:4.0.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.18.2'
    implementation 'org.postgresql:postgresql:42.7.4'
}

application {
    mainClass = 'lab8.client.ClientMain'
}

jar {
    enabled = false 
}

tasks.register('clientJar', Jar) {
    archiveBaseName = 'ClientApp'
    manifest {
        attributes 'Main-Class': 'lab8.client.ClientMain'
    }
    from(sourceSets.main.output) {
        include 'lab8/client/**'
        include 'lab8/shared/**'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('serverJar', Jar) {
    archiveBaseName = 'ServerApp'
    manifest {
        attributes 'Main-Class': 'lab8.server.ServerMain'
    }
    from(sourceSets.main.output) {
        include 'lab8/server/**'
        include 'lab8/shared/**'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

run {
    standardInput = System.in
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.build {
    dependsOn 'clientJar'
    dependsOn 'serverJar'
}